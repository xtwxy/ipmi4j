buildscript {
	apply from: file('gradle/buildscript.gradle'), to: buildscript
}

apply plugin: 'org.anarres.stdproject'
apply plugin: 'eclipse'

stdproject {
	javadocLink "https://mina.apache.org/mina-project/apidocs/"
	javadocLink "http://netty.io/4.0/api/"
}

subprojects {
	group = "org.anarres.ipmi"

	apply plugin: 'org.anarres.stdmodule'
	stdmodule {
		description "A high-performance pure Java IPMI client."
		author id: 'shevek', name: 'Shevek', email: 'github@anarres.org'
		license 'Apache-2.0'
	}

	sourceCompatibility = 1.7
	animalsniffer {
		signature = "org.codehaus.mojo.signature:java17:+@signature"
	}
}

project(':ipmi-protocol') {
	dependencies {
		compile 'com.google.code.findbugs:annotations:2.0.3'
		compile 'org.slf4j:slf4j-api:1.7.12'
		compile 'com.google.guava:guava:18.0'
		compile 'io.netty:netty-all:4.0.23.Final'

		testCompile 'commons-net:commons-net:3.3'
		testCompile 'io.netty:netty-all:4.0.23.Final'

		// testCompile 'org.pcap4j:pcap4j-core:1.3.0'
		testCompile 'org.pcap4j:pcap4j-packetfactory-static:1.3.0'

		testCompile "org.apache.velocity:velocity:1.7"
		testCompile "org.apache.velocity:velocity-tools:2.0"
	}
}

project(':ipmi-client-mina') {
	dependencies {
		compile project(':ipmi-protocol')
		compile 'org.apache.mina:mina-core:2.0.8'

		testCompile 'commons-net:commons-net:3.3'
		testCompile project(':ipmi-protocol').sourceSets.test.output
	}
}

project(':ipmi-client-netty') {
	dependencies {
		compile project(':ipmi-protocol')
		compile 'io.netty:netty-all:4.0.23.Final'

		testCompile 'commons-net:commons-net:3.3'
		testCompile project(':ipmi-protocol').sourceSets.test.output

		testRuntime 'org.javassist:javassist:3.18.2-GA'
	}
}
